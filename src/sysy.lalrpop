// CompUnit      ::= [CompUnit] (Decl | FuncDef);
// 
// Decl          ::= ConstDecl | VarDecl;
// ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
// BType         ::= "int";
// ConstDef      ::= IDENT "=" ConstInitVal;
// ConstInitVal  ::= ConstExp;
// VarDecl       ::= BType VarDef {"," VarDef} ";";
// VarDef        ::= IDENT | IDENT "=" InitVal;
// InitVal       ::= Exp;
// 
// FuncDef       ::= FuncType IDENT "(" [FuncFParams] ")" Block;
// FuncType      ::= "void" | "int";
// FuncFParams   ::= FuncFParam {"," FuncFParam};
// FuncFParam    ::= BType IDENT;
// 
// Block         ::= "{" {BlockItem} "}";
// BlockItem     ::= Decl | Stmt;
// Stmt          ::= LVal "=" Exp ";"
//                 | [Exp] ";"
//                 | Block
//                 | "if" "(" Exp ")" Stmt ["else" Stmt]
//                 | "while" "(" Exp ")" Stmt
//                 | "break" ";"
//                 | "continue" ";"
//                 | "return" [Exp] ";";
// 
// Exp           ::= LOrExp;
// LVal          ::= IDENT;
// PrimaryExp    ::= "(" Exp ")" | LVal | Number;
// Number        ::= INT_CONST;
// UnaryExp      ::= PrimaryExp | IDENT "(" [FuncRParams] ")" | UnaryOp UnaryExp;
// UnaryOp       ::= "+" | "-" | "!";
// FuncRParams   ::= Exp {"," Exp};
// MulExp        ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
// AddExp        ::= MulExp | AddExp ("+" | "-") MulExp;
// RelExp        ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
// EqExp         ::= RelExp | EqExp ("==" | "!=") RelExp;
// LAndExp       ::= EqExp | LAndExp "&&" EqExp;
// LOrExp        ::= LAndExp | LOrExp "||" LAndExp;
// ConstExp      ::= Exp;

use crate::ast_type::*;

grammar;

match {
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
    r"/\*([^\*]|\*+[^/\*])*\*+/" => {},
    _
}

pub CompUnit: CompUnit = <comp_unit_items: CompUnitItem*> => CompUnit { <> };

CompUnitItem: CompUnitItem = {
    <Decl> => CompUnitItem::Decl(<>),
    <FuncDef> => CompUnitItem::FuncDef(<>),
}

Decl: Decl = {
    <ConstDecl> => Decl::Const(<>),
    <VarDecl> => Decl::Var(<>),
}

ConstDecl: ConstDecl = {
    "const" "int" <mut const_defs: (<ConstDef> ",")*> <const_def: ConstDef> ";" => {
        const_defs.push(const_def);
        ConstDecl { const_defs }
    },
}

ConstDef: ConstDef = <ident: IDENT> "=" <const_init_val: ConstInitVal> => ConstDef { <> };

ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal { <> };

VarDecl: VarDecl = {
    "int" <mut var_defs: (<VarDef> ",")*> <var_def: VarDef> ";" => {
        var_defs.push(var_def);
        VarDecl { var_defs }
    },
}

VarDef: VarDef = {
    <ident: IDENT> <init_val: ("=" <InitVal>)?> => VarDef { <> },
}

InitVal: InitVal = <exp: Exp> => InitVal { <> };

FuncDef: FuncDef = {
    "void" <ident: IDENT> "(" <params: FuncFParams?> ")" <block: Block> => {
        FuncDef { func_type: FuncType::Void, <> }
    },
    "int" <ident: IDENT> "(" <params: FuncFParams?> ")" <block: Block> => {
        FuncDef { func_type: FuncType::Int, <> }
    },
}

FuncFParams: FuncFParams = {
    <mut params: (<FuncFParam> ",")*> <param: FuncFParam> => {
        params.push(param);
        FuncFParams { params }
    },
}

FuncFParam: FuncFParam = "int" <ident: IDENT> => FuncFParam { <> };

Block: Block = "{" <block_items: BlockItem*> "}" => Block { <> };

BlockItem: BlockItem = {
    <Decl> => BlockItem::Decl(<>),
    <Stmt> => BlockItem::Stmt(<>),
}

Stmt: Stmt = {
    <OpenStmt> => <>,
    <ClosedStmt> => <>,
}

OpenStmt: Stmt = {
    "if" "(" <exp: Exp> ")" <stmt: Stmt> => Stmt::If(exp, Box::new(stmt), None),
    "if" "(" <exp: Exp> ")" <closed_stmt: ClosedStmt> "else" <open_stmt: OpenStmt> => {
        Stmt::If(exp, Box::new(closed_stmt), Some(Box::new(open_stmt)))
    },
    "while" "(" <exp: Exp> ")" <open_stmt: OpenStmt> => {
        Stmt::While(exp, Box::new(open_stmt))
    },
}

ClosedStmt: Stmt = {
    <SimpleStmt> => <>,
    "if" "(" <exp: Exp> ")" <closed_stmt1: ClosedStmt> "else" <closed_stmt2: ClosedStmt> => {
        Stmt::If(exp, Box::new(closed_stmt1), Some(Box::new(closed_stmt2)))
    },
    "while" "(" <exp: Exp> ")" <closed_stmt: ClosedStmt> => {
        Stmt::While(exp, Box::new(closed_stmt))
    },
}

SimpleStmt: Stmt = {
    <LVal> "=" <Exp> ";" => Stmt::Assign(<>),
    <Exp?> ";" => Stmt::Exp(<>),
    <Block> => Stmt::Block(<>),
    "break" ";" => Stmt::Break,
    "continue" ";" => Stmt::Continue,
    "return" <Exp?> ";" => Stmt::Return(<>),
}

Exp: Exp = <lor_exp: LOrExp> => Exp { <> };

LVal: LVal = <ident: IDENT> => LVal { <> };

PrimaryExp: PrimaryExp = {
    "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new(exp)),
    <LVal> => PrimaryExp::LVal(<>),
    <Number> => PrimaryExp::Num(<>),
}

Number: Number = <INT_CONST> => Number::IntConst(<>);

UnaryExp: UnaryExp = {
    <PrimaryExp> => UnaryExp::Primary(<>),
    <IDENT> "(" <FuncRParams?> ")" => UnaryExp::Func(<>),
    <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::Unary(unary_op, Box::new(unary_exp)),
}

UnaryOp: UnaryOp = {
    "+" => UnaryOp::Pos,
    "-" => UnaryOp::Neg,
    "!" => UnaryOp::Not,
}

FuncRParams: FuncRParams = {
    <mut params: (<Exp> ",")*> <param: Exp> => {
        params.push(param);
        FuncRParams { params }
    },
}

MulExp: MulExp = {
    <UnaryExp> => MulExp::Unary(<>),
    <mul_exp: MulExp> <mul_op: MulOp> <unary_exp: UnaryExp> => MulExp::Mul(Box::new(mul_exp), mul_op, unary_exp),
}

MulOp: MulOp = {
    "*" => MulOp::Mul,
    "/" => MulOp::Div,
    "%" => MulOp::Mod,
}

AddExp: AddExp = {
    <MulExp> => AddExp::Mul(<>),
    <add_exp: AddExp> <add_op: AddOp> <mul_exp: MulExp> => AddExp::Add(Box::new(add_exp), add_op, mul_exp),
}

AddOp: AddOp = {
    "+" => AddOp::Add,
    "-" => AddOp::Sub,
}

RelExp: RelExp = {
    <AddExp> => RelExp::Add(<>),
    <rel_exp: RelExp> <rel_op: RelOp> <add_exp: AddExp> => RelExp::Rel(Box::new(rel_exp), rel_op, add_exp),
}

RelOp: RelOp = {
    "<" => RelOp::Lt,
    ">" => RelOp::Gt,
    "<=" => RelOp::Le,
    ">=" => RelOp::Ge,
}

EqExp: EqExp = {
    <RelExp> => EqExp::Rel(<>),
    <eq_exp: EqExp> <eq_op: EqOp> <rel_exp: RelExp> => EqExp::Eq(Box::new(eq_exp), eq_op, rel_exp),
}

EqOp: EqOp = {
    "==" => EqOp::Eq,
    "!=" => EqOp::NotEq,
}

LAndExp: LAndExp = {
    <EqExp> => LAndExp::Eq(<>),
    <land_exp: LAndExp> "&&" <eq_exp: EqExp> => LAndExp::LAnd(Box::new(land_exp), eq_exp),
}

LOrExp: LOrExp = {
    <LAndExp> => LOrExp::LAnd(<>),
    <lor_exp: LOrExp> "||" <land_exp: LAndExp> => LOrExp::LOr(Box::new(lor_exp), land_exp),
}

ConstExp: ConstExp = <exp: Exp> => ConstExp { <> };

IDENT: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

INT_CONST: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
    r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
    r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
